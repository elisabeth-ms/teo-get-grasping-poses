include(GitInfo) # YCM

# Define current version.
set(TEO_GET_GRASPING_POSES_VERSION_SHORT ${TEO_GET_GRASPING_POSES_VERSION})

# Retrieve current revision from Git working tree.
git_wt_info(SOURCE_DIR "${CMAKE_SOURCE_DIR}"
            PREFIX TEO_GET_GRASPING_POSES)

if(DEFINED TEO_GET_GRASPING_POSES_GIT_WT_HASH)
    if(TEO_GET_GRASPING_POSES_GIT_WT_TAG_REVISION GREATER 0)
        set(TEO_GET_GRASPING_POSES_VERSION_REVISION ${TEO_GET_GRASPING_POSES_GIT_WT_TAG_REVISION})
        string(REPLACE "-" "" _date ${TEO_GET_GRASPING_POSES_GIT_WT_AUTHOR_DATE})
        set(TEO_GET_GRASPING_POSES_VERSION_SOURCE
            "${_date}.${TEO_GET_GRASPING_POSES_GIT_WT_DATE_REVISION}+git${TEO_GET_GRASPING_POSES_GIT_WT_HASH_SHORT}")
    endif()

    if(TEO_GET_GRASPING_POSES_GIT_WT_DIRTY)
        set(TEO_GET_GRASPING_POSES_VERSION_DIRTY "dirty")
    endif()
endif()

if(DEFINED TEO_GET_GRASPING_POSES_VERSION_SOURCE)
    if(NOT "${TEO_GET_GRASPING_POSES_GIT_WT_TAG}" STREQUAL "v${TEO_GET_GRASPING_POSES_VERSION_SHORT}")
        set(TEO_GET_GRASPING_POSES_VERSION
            "${TEO_GET_GRASPING_POSES_VERSION_SHORT}+${TEO_GET_GRASPING_POSES_VERSION_SOURCE}")
    else()
        set(TEO_GET_GRASPING_POSES_VERSION
           "${TEO_GET_GRASPING_POSES_VERSION_SHORT}+${TEO_GET_GRASPING_POSES_VERSION_REVISION}-${TEO_GET_GRASPING_POSES_VERSION_SOURCE}")
    endif()
elseif(NOT "${TEO_GET_GRASPING_POSES_GIT_WT_TAG}" STREQUAL "v${TEO_GET_GRASPING_POSES_VERSION_SHORT}")
    set(TEO_GET_GRASPING_POSES_VERSION "${TEO_GET_GRASPING_POSES_VERSION_SHORT}~dev")
else()
    set(TEO_GET_GRASPING_POSES_VERSION "${TEO_GET_GRASPING_POSES_VERSION_SHORT}")
endif()

if(DEFINED TEO_GET_GRASPING_POSES_VERSION_DIRTY)
    set(TEO_GET_GRASPING_POSES_VERSION "${TEO_GET_GRASPING_POSES_VERSION}+${TEO_GET_GRASPING_POSES_VERSION_DIRTY}")
endif()

# Print version.
message(STATUS "TEO_GET_GRASPING_POSES version: ${TEO_GET_GRASPING_POSES_VERSION_SHORT} (${TEO_GET_GRASPING_POSES_VERSION})")
